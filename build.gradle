buildscript {
	ext {
		springBootVersion = '1.5.8.RELEASE'
		gradleGitPropertiesVersion = '1.4.17'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://plugins.gradle.org/m2" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'

group = "io.honeymon.learn"
version = "${jar.version}"

jar {
	baseName = 'springboot-orm-learn'
	version = '0.0.1-SNAPSHOT'
	manifest {
        attributes("Implementation-Title": "${jar.baseName}",
                   "Implementation-Version": "${jar.version}")
    }
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
	compile.exclude module: 'tomcat-jdbc'
	compile.exclude module: "commons-logging"
    querydslapt
}


dependencies {
	compile "org.springframework.boot:spring-boot-starter-web"
	compile "org.springframework.boot:spring-boot-devtools"
	compile "org.springframework.boot:spring-boot-starter-data-jpa"
	compile "org.springframework.boot:spring-boot-starter-thymeleaf"
	compile "org.springframework.boot:spring-boot-actuator"
	
	/**
	 * http://www.querydsl.com/
	 * 쿼리와 닮은 Java 메서드를 이용해서 사용하는 프레임워크.
	 * xml 등으로 쿼리를 작성할 필요가 없음
     */
    compile "com.querydsl:querydsl-jpa:4.1.4"
    compile ("com.querydsl:querydsl-apt:4.1.4") {
    		exclude group: "com.google.guava"
    }
    
    /**
     * HikariCP
     * https://github.com/brettwooldridge/HikariCP
     */
    compile "com.zaxxer:HikariCP"
	compile "com.h2database:h2"
	
	/**
     * http://www.projectlombok.org/
     * Java 클래스에서 자주생성하는 getter/setter 등의 메서드를 필드에 @Getter @Setter
     * 선언하면 자동생성해주는 프레임워크
     */
    compile "org.projectlombok:lombok"
    
    // webjars
    compile 'org.webjars:jquery:2.2.1'
    compile 'org.webjars:bootstrap:3.3.6'
	
	testCompile "com.jayway.jsonpath:json-path"
	testCompile "org.springframework.boot:spring-boot-starter-test" 
}


[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets {
    querydsl {
        java {
            srcDirs = ['src/main/querydsl']
        }
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
        '-proc:only', '-processor', 'com.querydsl.apt.jpa.JPAAnnotationProcessor'
    ]
    destinationDir = sourceSets.querydsl.java.srcDirs.iterator().next()
}
generateQueryDSL.mustRunAfter clean


compileJava {
	sourceCompatibility = 1.8
	dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileQuerydslJava {
	dependsOn generateQueryDSL
	options.warnings = false
	classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.querydsl.java.srcDirs
}